class CopyToClipboard
  constructor: (text) ->
    @text = text
    @copy()

  copy: () ->
    textarea = document.createElement("textarea")
    textarea.textContent = @text
    textarea.style.position = "fixed"  # Prevent scrolling to bottom of page in MS Edge.
    document.body.appendChild(textarea)
    textarea.select()
    try
      document.execCommand("copy")  # Security exception may be thrown by some browsers.
    catch
      console.warn("Copy to clipboard failed.")
      false
    finally
      document.body.removeChild(textarea);

class Modal
  constructor: (el) ->
    @modal = el
    @modalOverlay = el.parent(".modal-overlay")

  show: ->
    @modalOverlay.toggleClass("show")
    @

  close: ->
    @modalOverlay.toggleClass("show")

  modalBody: ->
    @modal.find('.modal__body')

  showWithHTML: (html) ->
    @modalBody().html(html)

  waiting: ->
    @modalBody().html """
      <div class="modal__spinner--overlay flex items-center justify-center">
        <%= image_tag('loader.gif', class: 'modal__spinner ma' ) %>
      </div>
    """


contactModalObj =
  el: "#contact-modal-container"
  data:
    showModal: false
    isLoading: false
    contact: {}
  methods:
    copyAddress: ({ street_1, street_2, city, state, zipcode }) ->
      text = "#{street_1}"
      text += "\n#{street_2}" if street_2
      text += "\n#{city}, #{state} #{zipcode}"
      new CopyToClipboard(text)
    showContactWithId: (id) ->
      self = @
      self.showModal = true
      self.isLoading = true
      TMLAxios.get "/contacts/#{id}.json"
      .then (response) ->
        self.isLoading = false
        self.contact = response.data.contact
        new ContactMap(document.getElementById("contact-card__map"), response.data.contact.address).show()


document.addEventListener "turbolinks:load", ->
  window.TMLContactModal = new Vue(contactModalObj)

window.addEventListener 'keyup', (evt) ->
  if evt.keyCode is 27
    TMLContactModal.showModal = false
